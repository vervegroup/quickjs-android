plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
}

android {
    namespace 'com.verve.shiqi.quickjs'
    compileSdk 33

    defaultConfig {
        minSdk 21
        targetSdk 33
        versionCode 4
        versionName "1.3"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }

        // build quickjs
        externalNativeBuild {
            cmake {
                targets 'quickjs-android'
                arguments '-DCMAKE_VERBOSE_MAKEFILE:BOOL=ON'
            }
        }
        ndk {
            //noinspection ChromeOsAbiSupport
            abiFilters 'armeabi-v7a', 'arm64-v8a'
        }
    }

    externalNativeBuild {
        cmake {
            path 'CMakeLists.txt'
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion '1.4.5'
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

publishing.publications {
    release(MavenPublication) {
        groupId project.group
        artifactId project.name
        version project.version

        project.afterEvaluate {
            artifact project.tasks.named("bundleReleaseAar").get()
        }

        pom.withXml {
            def dependencies = asNode().appendNode('dependencies')
            project.configurations.api.dependencies.withType(org.gradle.api.internal.artifacts.dependencies.AbstractModuleDependency).each {
                def dependency = dependencies.appendNode('dependency')
                dependency.appendNode('groupId', it.group)
                dependency.appendNode('artifactId', it.name)
                dependency.appendNode('version', it.version)
                dependency.appendNode('scope', "compile")
            }
        }
    }
}

dependencies {
    implementation 'androidx.annotation:annotation:1.6.0'
}